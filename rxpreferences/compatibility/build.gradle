import me.champeau.gradle.japicmp.JapicmpTask

configurations {
  baseline
  latest
}

dependencies {
  baseline('drewhamilton.rxpreferences:rxpreferences:1.0.0-alpha3') {
    transitive = false
    force = true
  }
  latest project(path: ':rxpreferences', configuration: 'releaseRuntimeElements')
}

task japicmp(type: JapicmpTask) {
  oldClasspath = configurations.baseline.incoming.artifactView { config ->
    config.attributes { container ->
      container.attribute(Attribute.of("artifactType", String.class), "jar")
    }
  }.artifacts.artifactFiles

  newClasspath = configurations.latest.incoming.artifactView { config ->
    config.attributes { container ->
      container.attribute(Attribute.of("artifactType", String.class), "jar")
    }
  }.artifacts.artifactFiles

  onlyBinaryIncompatibleModified = true
  failOnModification = true
  txtOutputFile = file("$buildDir/reports/japi.txt")
  ignoreMissingClasses = true
  includeSynthetic = true

  dependsOn(':rxpreferences:assembleRelease')
}

def check = tasks.create('check')
check.dependsOn(japicmp)

def build = tasks.create('build')
build.dependsOn(check)
