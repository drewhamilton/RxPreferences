buildscript {
    ext {
        kotlin_version = '1.3.41'
        japicmp_version = '0.2.8'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'

        classpath "me.champeau.gradle:japicmp-gradle-plugin:$japicmp_version"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"

        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }

    repositories {
        google()
        gradlePluginPortal()
        jcenter()
    }
}

apply from: 'publish.gradle'
apply from: 'compatibility.gradle'

allprojects { project ->
    repositories {
        google()
        gradlePluginPortal()
        jcenter()
    }

    //noinspection UnnecessaryQualifiedReference
    project.plugins.withType(com.android.build.gradle.LibraryPlugin) {
        project.android.libraryVariants.all { variant ->
            // TODO WORKAROUND: replace with https://issuetracker.google.com/issues/72050365 once released.
            variant.generateBuildConfigProvider.get().enabled = false
        }
    }
}

ext {
    libraryVersion = '3.0.0-alpha01'

    targetSdk = 28
    minSdk = 14

    rxJavaVersion = '3.0.0-RC1'

    keystore = project.hasProperty('personalKeystore') ? personalKeystore : 'x'
    keystorePassword = project.hasProperty('personalKeystorePassword') ? personalKeystorePassword : 'x'
    keyAlias = project.hasProperty('personalKeyAlias') ? personalKeyAlias : 'x'
    keyPassword = project.hasProperty('personalKeyPassword') ? personalKeyPassword : 'x'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
