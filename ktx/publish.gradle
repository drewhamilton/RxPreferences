apply from: '../publish.gradle'

ext {
  bintrayName = 'RxPreferences-KTX'
  libraryName = 'RxPreferences-KTX'
  artifact = 'rxpreferences-ktx'

  libraryDescription = 'Kotlin extensions for RxPreferences.'
}

group = publishedGroupId
version = libraryVersion
def javadocDestinationDir = "$buildDir/docs/javadoc"

install {
  repositories.mavenInstaller {
    pom.project {
      packaging 'aar'
      groupId publishedGroupId
      artifactId artifact

      name libraryName
      description libraryDescription
      url siteUrl

      licenses {
        license {
          name licenseName
          url licenseUrl
        }
      }
      developers {
        developer {
          id developerId
          name developerName
          email developerEmail
        }
      }
      scm {
        connection gitUrl
        developerConnection gitUrl
        url siteUrl
      }
    }
  }
}

apply plugin: 'org.jetbrains.dokka-android'
task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

task dokkaJavadoc(type: dokka.class) {
  outputFormat = 'javadoc'
  outputDirectory = javadocDestinationDir
  inputs.dir 'src/main/java'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
  classifier = 'javadoc'
  from javadocDestinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

bintray {
  user = bintrayUser
  key = bintrayKey

  configurations = ['archives']
  pkg {
    repo = bintrayRepo
    name = bintrayName
    desc = libraryDescription
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = allLicenses
    dryRun = false
    publish = true
    override = false
    publicDownloadNumbers = true
    version {
      desc = libraryDescription
    }
  }
}
